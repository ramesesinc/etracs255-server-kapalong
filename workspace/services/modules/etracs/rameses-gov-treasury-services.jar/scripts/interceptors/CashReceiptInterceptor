import com.rameses.annotations.*;

class CashReceiptInterceptor {

	@DataContext('collectiontype')
	def colltype_db;

	@DataContext('checkpayment')
	def check_db;

	@DataContext('eftpayment')
	def eft_db;

	@Service("NumberService")
	def numSvc;

	@After(pattern="PersistenceService.create", eval="#{ args[0]._schemaname == 'cashreceipt' }", index=-10000)
	public void afterCreate( evt ) { 
		adjustReceipt( evt.result ); 
	} 


	@After(pattern="PersistenceService.read", eval="#{ args[0]._schemaname == 'cashreceipt' }", index=-10000)
	public void afterOpen( evt ) { 
		adjustReceipt( evt.result ); 
	} 

	void adjustReceipt( data ) { 
		if ( data == null ) return; 

		if ( data.collectiontype?.objid ) { 
			def cta = colltype_db.find([ objid: data.collectiontype.objid ]).select('name,title,handler,formno').first(); 
			if ( cta ) data.collectiontype = cta; 
		} 

		data.paymentitems.each{ 
			if ( it.reftype.toString().toUpperCase() == 'CHECK') {
				def fldnames = 'objid,refno,refdate,bank.*';
				def chk = check_db.find([ objid: it.refid ]).select( fldnames ).first(); 
				if ( chk ) it.check = chk;
			}
			else if ( it.reftype.toString().toUpperCase() == 'EFT') {
				def fldnames = 'objid,refno,refdate,bankaccount.bank.*';
				def chk = eft_db.find([ objid: it.refid ]).select( fldnames ).first(); 
				if ( chk ) {
					it.check = chk;
					it.check.bank = chk.bankaccount?.bank;
				}
			} 
		}
		
		data.payerdetails = data.paidby + "\n" + data.paidbyaddress; 
		if ( data?.amount instanceof Number ) { 
			if( data.amount < 1 ) { 
				def amount = Long.parseLong( data.amount.toString().split("\\.")[1] ); 
				data.amountinwords = numSvc.doubleToWords( amount ).toUpperCase() + " CENTAVOS"; 
			} else { 
				data.amountinwords = numSvc.doubleToWords( data.amount ).toUpperCase() + " PESOS ONLY";
			} 
		} 

		// data compliant to previous versions
		if( data.totalnoncash == null ) data.totalnoncash = data.totalcheck; 
	}
}